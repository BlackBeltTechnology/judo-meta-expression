import "../data/entity.eol";

@cached
operation EXPR!SingleEntitySelectorExpression prepare() : JUDOPSM!Type {
	if (not self.entityType.isDefined()) {
		self.entityType = self.entitySetExpression.prepare();
	}
	
	return self.entityType;
}

@cached
operation EXPR!EntityNavigationExpression prepare() : JUDOPSM!Type {
	if (not self.entityType.isDefined() and self.entityExpression.isDefined()) {
		var base = self.entityExpression.prepare();

		var ref = base.getReferenceByName(self.singleReferenceName);
        
        if (ref.isDefined()) {
            self.entityType = ref.target;
        } else {
            ("Reference named " + self.singleReferenceName + " not found in entity " + self.name + " (or its supertypes)").println();
        }
	}
	
	return self.entityType;
}

@cached
operation EXPR!EntityFilterExpression prepare() : JUDOPSM!Type {
	if (not self.entityType.isDefined() and self.entityExpression.isDefined()) {
	   var base = self.entityExpression.prepare();
        var filter = self.filter;
        
        if (filter.isDefined()) {
            var filterAndCastingType = filter.filterAndCastingType;
            if (filterAndCastingType.isDefined()) {
                var et = filterAndCastingType.getEntityType();
                
                var isSubClass = false;
                var super = et.superEntityTypes;
                while (super.isDefined() and not super.isEmpty() and not isSubClass) {
                    if (super.contains(base)) {
                        isSubClass = true;
                    } else {
                        var super2 = new Sequence;
                        for (s in super) {
                            super2.addAll(s.superEntityTypes);
                        }
                        super = super2;
                    }
                }
                
                if (isSubClass) {
                    self.entityType = et;
                } else {
                    (et.name + " is not superEntityType of " + base.name + " so lambda type filter/casting is invalid").println();
                }
            } else {
                self.entityType = base;
            }
            
            var expression = filter.expression;
        }
	}
	
	return self.entityType;
}


@cached
operation EXPR!Lambda prepare() : JUDOPSM!Type {
    if (not self.entityType.isDefined()) {
        var entityExpression = EXPR!EntityFilterExpression.all.selectOne(e | e.filter = self);
        var entitySetExpression = EXPR!EntitySetFilterExpression.all.selectOne(e | e.filter = self);
        
        if (entityExpression.isDefined()) {
            self.entityType = entityExpression.prepare();
        } else if (entitySetExpression.isDefined()) {
            self.entityType = entitySetExpression.prepare();
        } else {
            ("Could not detect type of lambda expression: " + self.name).println();
        }
    }
    
    return self.entityType;
}

@cached
operation EXPR!EntityVariableReference prepare() : JUDOPSM!Type {
    if (not self.entityType.isDefined() and self.variable.isDefined()) {
        self.entityType = self.variable.prepare();
    }
    
    return self.entityType;
}