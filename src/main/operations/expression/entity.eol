import "../data/psm.eol";

@cached
operation EXPR!EntityExpression getEntityType() {
    throw "Unable to detect type";
}

@cached
operation EXPR!SingleEntitySelectorExpression getEntityType() {
    return self.entitySetExpression.getEntityType();
}

@cached
operation EXPR!EntityFilterExpression getEntityType() {
    var base = self.entityExpression.getEntityType();
    if (base.isDefined()) {
        base.name.println(" - base: ");
        
        if (self.filter.isDefined()) {
            self.filter.type().name.println(" - filter: ");
            
            if (self.filter.filterAndCastingType.isDefined()) {
                return self.filter.filterAndCastingType.getEntityType();
            } else {
                return base;
            }
        } else {
            return base;
        }
    }
}

@cached
operation EXPR!EntityNavigationExpression getEntityType() {
    return self.entityExpression.getEntityType().getReferenceType(self.singleReferenceName);
}

@cached
operation EXPR!EntityVariableReference getEntityType() {
    return self.variable.getEntityType();
}

@cached
operation EXPR!Attribute getAttributeType() {
    return self.entityExpression.getEntityType().getAttributeType(self.attributeName);
}
